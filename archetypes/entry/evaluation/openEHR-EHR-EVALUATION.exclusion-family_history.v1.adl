archetype (adl_version=1.4)
    openEHR-EHR-EVALUATION.exclusion-family_history.v1
specialize
    openEHR-EHR-EVALUATION.exclusion.v1

concept
    [at0000.1]

language
    original_language = <[ISO_639-1::en]>

description
    original_author = <
        ["date"] = <"2010-03-2">
        ["name"] = <"Heather Leslie">
        ["organisation"] = <"Ocean Informatics">
        ["email"] = <"heather.leslie@oceaninformatics.com">
    >
    lifecycle_state = <"AuthorDraft">
    details = <
        ["en"] = <
            language = <[ISO_639-1::en]>
            purpose = <"To positively record statement/s about family history that need to be clinically excluded from the health record at a specific point in time.">
            keywords = <"exclusion","family">
            copyright = <"Â© openEHR Foundation">
            use = <"Use to record positive statement/s about the absence or exclusion of family history information within the health record at a specific point in time. For example: to record that the subject does not have any significant family history; or that the subject has no family members with heart disease.

This archetype has specifically been designed to avoid the need to use flags or terminology to express negation about any family history within the health record. 

Exclusion statements can only be considered to be current and accurate at the time of recording. For example: recording an exclusion is only relevant at the time of recording and may be effectively obsolete immediately after the record is made if new information is discovered.">
            misuse = <"Not to be used to record the exclusion or absence of adverse reactions, medication use, procedures, problems or diagnoses - use specific specialisations of this archetype for this purpose.">
        >
    >
    other_details = <
        ["references"] = <"Exclusion of Family History, draft archetype, NEHTA Clinical Knowledge Manager [Internet]. NEHTA. Authored: 02 Mar 2010. Available at: 
http://dcm.nehta.org.au/ckm/#showArchetype_1013.1.1314_1(accessed 3 Sept 2013)">
        ["current_contact"] = <"Heather Leslie, Ocean Informatics, heather.leslie@oceaninformatics.com">
        ["MD5-CAM-1.0.1"] = <"A7EB66A13879B784929B4EA2915C38C9">
    >

definition
    EVALUATION[at0000.1] matches {
        data matches {
            ITEM_TREE[at0001] matches {
                items existence matches {0..1} cardinality matches {1..*; unordered} matches {
                    ELEMENT[at0002] occurrences matches {1..*} matches {
                        value existence matches {0..1} matches {
                            DV_TEXT matches {*}
                        }
                    }
                    ELEMENT[at0003.1] occurrences matches {0..*} matches {
                        value existence matches {0..1} matches {
                            DV_TEXT matches {*}
                            DV_CODED_TEXT matches {
                                defining_code matches {
                                    [ac0.1]
                                }
                            }
                        }
                    }
                    ELEMENT[at0007] occurrences matches {0..1} matches {
                        value existence matches {0..1} matches {
                            DV_TEXT matches {*}
                        }
                    }
                }
            }
        }
        protocol existence matches {0..1} matches {
            ITEM_TREE[at0006] matches {
                items existence matches {0..1} cardinality matches {0..*; unordered} matches {
                    ELEMENT[at0004] occurrences matches {0..1} matches {
                        value existence matches {0..1} matches {
                            DV_DATE_TIME matches {*}
                        }
                    }
                }
            }
        }
    }

ontology
    terminologies_available = <"en", ...>
    term_definitions = <
        ["en"] = <
            items = <
                ["at0000.1"] = <
                    text = <"Exclusion of Family History">
                    description = <"Positive statement/s about family history that need to be recorded as clinically excluded from the health record at a specific point in time.">

                >
                ["at0001"] = <
                    text = <"Tree">
                    description = <"@ internal @">

                >
                ["at0002"] = <
                    text = <"Exclusion Statement">
                    description = <"The statement about an exclusion in the health record.">
                    comment = <"Use to capture any information that is needed to be positively recorded as being excluded within the record. The statement can support recording global statements or statements about a specified object. See specialisations for Medications, Adverse Reactions, Problem/Diagnosis and Procedures as common examples that have been explicitly modelled.">

                >
                ["at0003.1"] = <
                    text = <"Family History">
                    description = <"Identification of the specific fanily history to which the Exclusion Statement applies.">

                >
                ["at0004"] = <
                    text = <"Date Last Updated">
                    description = <"The date at which the exclusion was last clinically asserted, affirmed or confirmed.">

                >
                ["at0006"] = <
                    text = <"Tree">
                    description = <"@ internal @">

                >
                ["at0007"] = <
                    text = <"Comment">
                    description = <"Additional narrative about the Exclusion not captured in other fields.">

                >
            >
        >
    >
    constraint_definitions = <
        ["en"] = <
            items = <
                ["ac0.1"] = <
                    text = <"Family History">
                    description = <"All possible findings of Family History">

                >
            >
        >
    >
